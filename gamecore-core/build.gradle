plugins {
    id 'java'
    // Shadow
    id "io.github.goooler.shadow" version "8.1.7"
}

group = 'net.malfact'
version = '1.2-SNAPSHOT'

layout.buildDirectory.set(rootProject.layout.buildDirectory)

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

repositories {
    mavenCentral()
    // Paper
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // CommandAPI
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
    // PlaceholderAPI
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // TAB
//    maven { url 'https://jitpack.io' }
    // WorldGuard
    maven { url "https://maven.enginehub.org/repo/" }

    // Luaj2
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/malfact/luaj")
        credentials {
            username = properties.getProperty("gpr.user")
            password = properties.getProperty("gpr.key")
        }
    }
}

def luaj2 = "net.malfact.luaj:luaj2:1.1.4-SNAPSHOT"

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly "dev.jorel:commandapi-bukkit-core:9.5.1"
//    compileOnly 'com.github.NEZNAMY:TAB-API:4.1.6'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.10'
    implementation luaj2
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    dependencies {
        include(dependency(luaj2))
    }
    archiveClassifier.set('')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.register('deleteOldCopy', Delete) {
    def copyToDir = properties.getProperty("copy-build-dir")
    if (copyToDir == null)
        return

    delete(fileTree(copyToDir).matching {
        //noinspection GrUnresolvedAccess
        include "${rootProject.name}**.jar"
    }.files)

    println("Deleted old Jars")
}

tasks.register('copyJarTo', Copy) {
    def copyToDir = properties.getProperty("copy-build-dir")
    if (copyToDir == null)
        return

    dependsOn(deleteOldCopy)
    dependsOn(shadowJar)
    from shadowJar
    into copyToDir
    println("Copied Jars to ${copyToDir}")
}